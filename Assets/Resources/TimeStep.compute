#define NUM_THREADS 1024

static const float Epsilon = 1e-5;

struct ShaderParameters
{
    uint _MaxL;
    uint _TimeStep;
};

#pragma enable_d3d11_debug_symbols

// Buffers
StructuredBuffer<float2> _VelocityBuffer;
StructuredBuffer<float> _WaterDepthBuffer;
StructuredBuffer<float> _SmoothingLengthBuffer;
RWStructuredBuffer<ShaderParameters> _ShaderParametersBuffer;

// Parameters
float _Gravity;
uint _N;

#pragma kernel CSMain

[numthreads(NUM_THREADS, 1, 1)]
void CSMain(uint3 id : SV_DispatchThreadID)
{
    // Check if we're within texture bounds
    if (id.x >= _N)
        return;

    uint i = id.x;

    float newTimeStep = _SmoothingLengthBuffer[i] / (sqrt(_Gravity * _WaterDepthBuffer[i]) + length(_VelocityBuffer[i]));

    // Atomically get the min value
    uint compareValue = _ShaderParametersBuffer[0]._TimeStep;
    uint outValue = 0;

    uint i_val = asuint(min(newTimeStep, asfloat(_ShaderParametersBuffer[0]._TimeStep)));

    [allow_uav_condition]
    while (true)
    {
        InterlockedCompareExchange(_ShaderParametersBuffer[0]._TimeStep, compareValue, i_val, outValue);
        if (outValue == compareValue)
            break;

        compareValue = outValue;
        i_val = asuint(min(newTimeStep, asfloat(outValue)));
    }
}
